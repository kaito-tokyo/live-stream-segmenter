# Maintainer: Kaito Udagawa <umireon at kaito dot tokyo>
pkgname=live-stream-segmenter-git
_pkgname="${pkgname%-git}"
pkgver=0.1.0
pkgrel=1
pkgdesc='Live Stream Segmenter for OBS Studio'
arch=('x86_64')
url="https://github.com/kaito-tokyo/${_pkgname}"
license=('GPL-3.0-or-later')
depends=('curl' 'fmt' 'nlohmann-json' 'obs-studio')
makedepends=('git' 'cmake' 'ninja')
provides=("${_pkgname}" "${_pkgname}-debug")
conflicts=("${_pkgname}" "${_pkgname}-debug")
source=("${_pkgname}::git+https://github.com/kaito-tokyo/${_pkgname}.git")
sha256sums=('SKIP')

pkgver() {
  cd "${_pkgname}"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}


build() {
  cd "${_pkgname}"
  cmake -B build -S . \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DENABLE_FRONTEND_API=ON \
    -DENABLE_QT=ON \
    -DUSE_PKGCONFIG=ON \
    -GNinja
  cmake --build build
}

package() {
  cd "${_pkgname}"
  DESTDIR="$pkgdir" cmake --install build
}
